plugins {
    id 'java'
    id 'maven-publish'
}

group = 'me.katsumag'
version = '2.0'

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    implementation 'org.jetbrains:annotations:19.0.0'
    compileOnly 'org.spigotmc:spigot:1.15.2-R0.1-SNAPSHOT'
    testCompileOnly 'org.spigotmc:spigot:1.15.2-R0.1-SNAPSHOT'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include('plugin.yml')
        expand('name': name,
                'version': version,
                'main': 'me.katsumag.itemactionslib.ItemActionsLib')
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'me.katsumag'
            artifactId = 'item-actions-lib'
            version = '2.0'
            from components.java
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://repo.katsumag.me/repository/maven-releases"
            def snapshotsRepoUrl = "https://repo.katsumag.me/repository/maven-snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username System.env["MAVEN_USER"]
                password System.env["MAVEN_PASSWORD"]
            }
        }
    }
}